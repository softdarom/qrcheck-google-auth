plugins {
    id "org.springframework.boot" version "2.4.5"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "java-library"
    id "jacoco"
}

group = "ru.softdarom.qrcheck"

bootJar {
    archiveFileName = "google-auth.jar"
}

configurations {
    annotationProcessor {
        extendsFrom compileOnly
    }

    testAnnotationProcessor {
        extendsFrom testCompileOnly
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    set("springCloudVersion", "2020.0.1")
    set("javaxVersion", "2.0.1.Final")
    set("micrometerVersion", "1.5.4")
    set("swaggerVersion", "1.5.9")
    set("logbookVersion", "2.6.2")
    set("groovyVersion", "3.0.7")
    set("junitVersion", "5.4.2")
    set("wiremockVersion", "2.27.2")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    api "org.springframework.boot:spring-boot-starter-oauth2-client"
    api "org.springframework.boot:spring-boot-starter-security"
    api "org.springframework.boot:spring-boot-starter-web"
    api "org.springframework.boot:spring-boot-starter-actuator"
    api "org.springframework.cloud:spring-cloud-starter-sleuth"
    api "org.springframework.cloud:spring-cloud-starter-openfeign"
    api "org.springframework.cloud:spring-cloud-starter-kubernetes-client-config"

    api "javax.validation:validation-api:${javaxVersion}"
    api "io.micrometer:micrometer-registry-prometheus:${micrometerVersion}"
    api "org.zalando:logbook-spring-boot-starter:${logbookVersion}"
    api "org.springdoc:springdoc-openapi-ui:${swaggerVersion}"

    runtimeOnly "org.codehaus.groovy:groovy-all:${groovyVersion}"

    compileOnly "org.projectlombok:lombok"
    compileOnly "org.springframework.boot:spring-boot-configuration-processor"

    testCompileOnly "org.projectlombok:lombok"
    testCompileOnly "org.springframework.boot:spring-boot-configuration-processor"

    developmentOnly "org.springframework.boot:spring-boot-devtools"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "com.github.tomakehurst:wiremock:${wiremockVersion}"
}

jacocoTestReport {
    reports {
        xml {
            enabled true
            destination file("${buildDir}/jacoco/coverage.xml")
        }
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = "LINE"
                value = "COVEREDRATIO"
                minimum = 0.8
            }
        }
    }
}

test {
    useJUnitPlatform {
        includeTags "spring-mock", "spring-integration", "unit"
    }
    finalizedBy(jacocoTestReport, jacocoTestCoverageVerification)
}
